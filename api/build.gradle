plugins {
    id "maven"
}

dependencies {
    compileOnly 'io.vertx:vertx-codegen:3.3.3'
    compileOnly 'io.vertx:vertx-rx-java:3.3.3'
    compileOnly 'io.vertx:vertx-service-discovery:3.3.3'
    
    compile 'com.amazonaws:aws-java-sdk-s3:1.11.41'
    compile 'org.slf4j:jcl-over-slf4j:1.7.5'
    compile 'org.apache.logging.log4j:log4j:2.5'
    compile 'org.slf4j:slf4j-log4j12:1.7.5'
    
    testCompile 'io.vertx:vertx-rx-java:3.3.3'
    //Spock Test Dependencies
    testCompile "org.codehaus.groovy:groovy-all:2.4.3"
    testCompile group: 'junit', name: 'junit', version: '4.11'
    testCompile "org.spockframework:spock-core:1.0-groovy-2.4"
    
    // optional dependencies for using Spock
    testCompile "org.hamcrest:hamcrest-core:1.3" // only necessary if Hamcrest matchers are used
    testRuntime "cglib:cglib-nodep:3.1"          // allows mocking of classes (in addition to interfaces)
    testRuntime "org.objenesis:objenesis:2.1"
}

task generateServiceProxy(type: JavaCompile, description: 'Generates EBServiceProxies') {
    source = sourceSets.main.java
    classpath = configurations.compile
    destinationDir = file("src/main/java")
    options.compilerArgs = ["-proc:only",
                            "-processor", "io.vertx.codegen.CodeGenProcessor",
                            "-AoutputDirectory=$destinationDir"]
}


task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives jar
    archives sourcesJar
    archives javadocJar
}


uploadArchives {
    repositories {
        mavenDeployer {
            repository(url: "http://dev.foreks.com/nexus/content/repositories/releases") {
                authentication(userName: nexusUsername, password: nexusPassword)
            }
            snapshotRepository(url: "http://dev.foreks.com/nexus/content/repositories/snapshots") {
                authentication(userName: nexusUsername, password: nexusPassword);
            }
            pom.project {
                name 'Log Collector'
                artifactId = 'log-collector'
                description 'This project aims to serve log files from local path or S3 over Vert.x eventbus'
                url 'https://dev.foreks.com/stash/scm/feed/log-collector.git'
                scm {
                    url 'https://dev.foreks.com/stash/scm/feed/log-collector.git'
                    connection 'scm:git:sercan.karaoglu@dev.foreks.com/stash/scm/feed/log-collector.git'
                    developerConnection 'scm:git:sercan.karaoglu@dev.foreks.com/stash/scm/feed/log-collector.git'
                }
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/license/LICENSE-2.0.txt'
                        distribution 'repo'
                    }
                }
                developers {
                    developer {
                        id 'SercanKaraoglu'
                        name 'Sercan Karaoglu'
                        email 'sercan.karaoglu@foreks.com'
                    }
                }
                
            }
        }
    }
}